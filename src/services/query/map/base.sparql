#defaultView:Map
SELECT DISTINCT
    ?item
    (SAMPLE(COALESCE(?langLabel,?defaultLabel,?anyLabel)) AS ?itemLabel)
    (SAMPLE(?itemDescription) AS ?itemDescription)
    (SAMPLE(?location) AS ?location)
    (SAMPLE(?commons) AS ?commons)
    (SAMPLE(?wikipedia) AS ?wikipedia)
    (SAMPLE(?picture) AS ?picture)
    (SAMPLE(?osm_node) AS ?osm_node)
    (SAMPLE(?osm_way) AS ?osm_way)
    (SAMPLE(?osm_rel) AS ?osm_rel)
WHERE {
    SERVICE wikibase:box {
        ?item wdt:P625 ?location.
        bd:serviceParam wikibase:cornerWest '${southWestWKT}'^^geo:wktLiteral .
        bd:serviceParam wikibase:cornerEast '${northEastWKT}'^^geo:wktLiteral .
    } # https://www.mediawiki.org/wiki/Wikidata_Query_Service/User_Manual#Search_within_box
    MINUS { ?item (wdt:P582|wdt:P585|wdt:P576|wdt:P3999) []. } # Ignore if the feature has an end date
    ?item rdfs:label ?anyLabel.
    OPTIONAL { ?item rdfs:label ?langLabel FILTER(LANG(?langLabel) = '${language}'). }
    OPTIONAL { ?item rdfs:label ?defaultLabel FILTER(LANG(?defaultLabel) = '${defaultLanguage}'). }
    OPTIONAL { ?item schema:description ?itemDescription FILTER(LANG(?itemDescription) = '${language}'). }
    OPTIONAL { ?item wdt:P373 ?commons }
    OPTIONAL { ?wikipedia schema:about ?item; schema:isPartOf <https://${language}.wikipedia.org/>. }
    OPTIONAL { ?item wdt:P18 ?picture }
    OPTIONAL { ?item wdt:P11693 ?osm_node. }
    OPTIONAL { ?item wdt:P10689 ?osm_way. }
    OPTIONAL { ?item wdt:P402 ?osm_rel. }
}
GROUP BY ?item
${limit}
