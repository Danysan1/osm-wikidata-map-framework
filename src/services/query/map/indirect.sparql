#defaultView:Map
SELECT
    ?etymology
    ?item
    (SAMPLE(?itemLabel) AS ?itemLabel)
    (SAMPLE(?location) AS ?location)
    (SAMPLE(?commons) AS ?commons)
    (SAMPLE(?wikipedia) AS ?wikipedia)
    (SAMPLE(?picture) AS ?picture)
    (SAMPLE(?osm_node) AS ?osm_node)
    (SAMPLE(?osm_way) AS ?osm_way)
    (SAMPLE(?osm_rel) AS ?osm_rel)
    (?etymology AS ?from_entity)
    (wdt:${indirectProperty} AS ?from_prop)
WITH { 
    SELECT ?etymology ?location ?commons ?picture ?osm_node ?osm_way ?osm_rel
    WHERE {
        ?etymology p:${indirectProperty} ?stmt.
        MINUS { ?stmt pq:P582 []. } # Ignore if it has an end date
        MINUS { ?etymology (wdt:${indirectProperty}/wdt:P373/^pq:P373) ?stmt} # Prevent duplicated etymologies when the etymology has another link that points to the exact entity for the item (see https://commons.wikimedia.org/wiki/Commons_talk:WikiProject_P%C3%A8re-Lachaise_Cemetery )
        SERVICE wikibase:box {
            ?stmt pq:P625 ?location.
            bd:serviceParam wikibase:cornerWest '${southWestWKT}'^^geo:wktLiteral;
                wikibase:cornerEast '${northEastWKT}'^^geo:wktLiteral.
        } # https://www.mediawiki.org/wiki/Wikidata_Query_Service/User_Manual#Search_within_box
        FILTER (isIRI(?etymology) && !wikibase:isSomeValue(?etymology))
        OPTIONAL { ?stmt pq:P373 ?commons. }
        ${pictureQuery}
        OPTIONAL { ?stmt pq:P11693 ?osm_node. }
        OPTIONAL { ?stmt pq:P10689 ?osm_way. }
        OPTIONAL { ?stmt pq:P402 ?osm_rel. }
    }
} AS %qualifier
WITH {
    SELECT ?etymology ?location ?item ?itemLabel ?commons ?wikipedia ?picture ?osm_node ?osm_way ?osm_rel
    WHERE {
        ?etymology p:${indirectProperty} ?stmt.
        MINUS { ?stmt (pq:P625|pq:P582) []. } # Ignore if it has a location qualifier (it will fetched be in the other subquery) or an end date
        MINUS { ?etymology (wdt:${indirectProperty}/wdt:P373/^pq:P373) ?stmt. } # Prevent duplicated etymologies when the etymology has another link that points to the exact entity for the item (see https://commons.wikimedia.org/wiki/Commons_talk:WikiProject_P%C3%A8re-Lachaise_Cemetery )
        ?stmt ps:${indirectProperty} ?item.
        MINUS { ?item (wdt:P582|wdt:P576|wdt:P3999) []. } # Ignore if it has a end date
        MINUS { ?etymology (wdt:${indirectProperty}/(wdt:P276|wdt:P195)) ?item. } # Prevent duplicated etymologies where both the item and the item's containing location are linked through the indirect property
        SERVICE wikibase:box {
            ?item wdt:P625 ?location.
            bd:serviceParam wikibase:cornerWest '${southWestWKT}'^^geo:wktLiteral;
                wikibase:cornerEast '${northEastWKT}'^^geo:wktLiteral.
        } # https://www.mediawiki.org/wiki/Wikidata_Query_Service/User_Manual#Search_within_box
        FILTER (isIRI(?etymology) && !wikibase:isSomeValue(?etymology))
        OPTIONAL { ?item wdt:P373 ?commons. }
        OPTIONAL { ?wikipedia schema:about ?item; schema:isPartOf <https://${language}.wikipedia.org/>. }
        OPTIONAL { ?item wdt:P18 ?picture. }
        OPTIONAL { ?item wdt:P11693 ?osm_node. }
        OPTIONAL { ?item wdt:P10689 ?osm_way. }
        OPTIONAL { ?item wdt:P402 ?osm_rel. }
        SERVICE wikibase:label {
            bd:serviceParam wikibase:language "${language},${defaultLanguage},en".
            ?item rdfs:label ?itemLabel.
        }
    }
} AS %reverse
WHERE {
    { INCLUDE %qualifier. } UNION { INCLUDE %reverse. }
}
GROUP BY ?item ?etymology
${limit}
