#defaultView:Map
SELECT DISTINCT
    ?item
    ?itemLabel
    ?itemDescription
    ?location
    ?commons
    ?wikipedia
    ?picture
    ?osm_node
    ?osm_way
    ?osm_rel
    ?etymology
    (?item AS ?from_entity)
    ?from_prop
WHERE {
    VALUES ?from_prop { ${directProperties} }
    ?item ?from_prop ?etymology.
    MINUS { ?item (wdt:P582|wdt:P576|wdt:P3999) []. } # Ignore if it has a end date
    SERVICE wikibase:box {
        ?item wdt:P625 ?location.
        bd:serviceParam wikibase:cornerWest '${southWestWKT}'^^geo:wktLiteral .
        bd:serviceParam wikibase:cornerEast '${northEastWKT}'^^geo:wktLiteral .
    } # https://www.mediawiki.org/wiki/Wikidata_Query_Service/User_Manual#Search_within_box
    FILTER (isIRI(?etymology) && !wikibase:isSomeValue(?etymology))
    OPTIONAL { ?item wdt:P373 ?commons }
    OPTIONAL { ?wikipedia schema:about ?item; schema:isPartOf <https://${language}.wikipedia.org/>. }
    OPTIONAL { ?item wdt:P18 ?picture }
    OPTIONAL { ?item wdt:P11693 ?osm_node. }
    OPTIONAL { ?item wdt:P10689 ?osm_way. }
    OPTIONAL { ?item wdt:P402 ?osm_rel. }
    SERVICE wikibase:label {
        bd:serviceParam wikibase:language "${language},${defaultLanguage},en".
        ?item rdfs:label ?itemLabel.
        ?item schema:description ?itemDescription.
    }
}
${limit}
