PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
SELECT
    ?item
    (SAMPLE(COALESCE(?langLabel,?defaultLabel,?anyLabel)) AS ?itemLabel)
    (SAMPLE(?itemDescription) AS ?itemDescription)
    (SAMPLE(?location) AS ?location)
    (SAMPLE(?commons) AS ?commons)
    (SAMPLE(?wikipedia) AS ?wikipedia)
    (SAMPLE(?picture) AS ?picture)
    (SAMPLE(?osm_node) AS ?osm_node)
    (SAMPLE(?osm_way) AS ?osm_way)
    (SAMPLE(?osm_rel) AS ?osm_rel)
WHERE {
    ?item p:P625 ?locationStatement;
          rdfs:label ?anyLabel.
    MINUS { ?item (wdt:P582|wdt:P585|wdt:P576|wdt:P3999) []. } # Ignore if the feature has an end date
    MINUS { ?locationStatement pq:P582 []. } # Ignore if the location statement has an end date
    ?locationStatement ps:P625 ?location.
    # FILTER(geo:sfContains( # https://opengeospatial.github.io/ogc-geosparql/geosparql11/spec.html#_f_2_1_query_parameters
    #     ?location,
    #     "POLYGON((${southLon} ${westLat}, ${southLon} ${eastLat}, ${northLon} ${eastLat}, ${northLon} ${westLat}, ${southLon} ${westLat}))"^^geo:wktLiteral
    # )) #! Not yet supported by QLever
    BIND ("POINT(${centerLon} ${centerLat})"^^geo:wktLiteral AS ?center)
    BIND (geof:distance(?location, ?center) AS ?distance)
    FILTER (?distance <= ${maxDistanceKm})
    OPTIONAL { ?item rdfs:label ?langLabel FILTER(LANG(?langLabel) = '${language}'). }
    OPTIONAL { ?item rdfs:label ?defaultLabel FILTER(LANG(?defaultLabel) = '${defaultLanguage}'). }
    OPTIONAL { ?item schema:description ?itemDescription FILTER(LANG(?itemDescription) = '${language}'). }
    OPTIONAL { ?item wdt:P373 ?commons. }
    OPTIONAL { ?wikipedia schema:about ?item; schema:isPartOf <https://${language}.wikipedia.org/>. }
    OPTIONAL { ?item wdt:P18 ?picture. }
    OPTIONAL { ?item wdt:P11693 ?osm_node. }
    OPTIONAL { ?item wdt:P10689 ?osm_way. }
    OPTIONAL { ?item wdt:P402 ?osm_rel. }
}
GROUP BY ?item
${limit}
