PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX ogc: <http://www.opengis.net/rdf#>
PREFIX osm: <https://www.openstreetmap.org/>
PREFIX osmkey: <https://www.openstreetmap.org/wiki/Key:>
PREFIX osmrel: <https://www.openstreetmap.org/relation/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
SELECT
    ?item
    ?osm
    (SAMPLE(COALESCE(?osm_location,?wd_location)) AS ?location)
    #(SAMPLE(COALESCE(?osm_lang_name,?wd_lang_label,?osm_default_name,?wd_default_label)) AS ?itemLabel)
    (SAMPLE(COALESCE(?wd_lang_label,?wd_default_label)) AS ?itemLabel)
    #(SAMPLE(COALESCE(?osm_descr,?wd_descr)) AS ?itemDescription)
    (SAMPLE(?wd_descr) AS ?itemDescription)
    (SAMPLE(?_commons) AS ?commons)
    (SAMPLE(?_wikipedia) AS ?wikipedia)
    (SAMPLE(?_picture) AS ?picture)
    #(BOUND(?osm) AS ?from_osm)
    #(BOUND(?item) AS ?from_wikidata)
WHERE {
    ?item p:P625 ?locationStatement.
    ${wikidataCountryQuery}
    MINUS { ?item (wdt:P582|wdt:P585|wdt:P576|wdt:P3999|wdt:P376) []. } # Ignore if the feature has an end date or isn't on Earth
    MINUS { ?locationStatement pq:P582 []. } # Ignore if the location statement has an end date
    ?locationStatement ps:P625 ?wd_location.

    SERVICE <https://qlever.cs.uni-freiburg.de/api/osm-planet> {
        ?osm osm:wikidata ?osm_wikidata; # osmkey:wikidata returns the tag value (Q-ID), osm:wikidata returns the URI
            geo:hasGeometry ?osm_location.
        ${osmCountryQuery}

        # Filter by location
        # ?osm_location geo:sfWithin "POLYGON((${westLon} ${southLat}, ${westLon} ${northLat}, ${eastLon} ${northLat}, ${eastLon} ${southLat}, ${westLon} ${southLat}))"^^geo:wktLiteral.
        FILTER (geof:distance(?osm_location, "POINT(${centerLon} ${centerLat})"^^geo:wktLiteral) <= ${maxDistanceKm})

        #OPTIONAL { ?osm <https://www.openstreetmap.org/wiki/Key:name:${language}> ?osm_lang_name. }
        #OPTIONAL { ?osm osmkey:name ?osm_default_name. }
        #OPTIONAL { ?osm osmkey:description ?osm_descr. }
    }

    OPTIONAL { ?osm_wikidata owl:sameAs ?item. }

    # Filter by location
    # COALESCE(?osm_location,?wd_location) geo:sfWithin "POLYGON((${westLon} ${southLat}, ${westLon} ${northLat}, ${eastLon} ${northLat}, ${eastLon} ${southLat}, ${westLon} ${southLat}))"^^geo:wktLiteral.
    BIND ("POINT(${centerLon} ${centerLat})"^^geo:wktLiteral AS ?center)
    FILTER (geof:distance(COALESCE(?osm_location,?wd_location), ?center) <= ${maxDistanceKm})

    OPTIONAL { ?item @${language}@rdfs:label ?wd_lang_label. }
    OPTIONAL { ?item @en@rdfs:label ?wd_default_label. }
    OPTIONAL { ?item @${language}@schema:description ?wd_descr. }
    OPTIONAL { ?item wdt:P373 ?_commons. }
    OPTIONAL { ?_wikipedia schema:about ?item; schema:isPartOf <https://${language}.wikipedia.org/>. }
    OPTIONAL { ?item wdt:P18 ?_picture. }
}
GROUP BY ?item ?osm
${limit}
