PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
SELECT
    ?item
    (SAMPLE(COALESCE(?langLabel,?defaultLabel,?anyLabel)) AS ?itemLabel)
    (SAMPLE(?_itemDescription) AS ?itemDescription)
    (SAMPLE(?_location) AS ?location)
    (SAMPLE(?_commons) AS ?commons)
    (SAMPLE(?_wikipedia) AS ?wikipedia)
    (SAMPLE(?_picture) AS ?picture)
    (SAMPLE(?_osm_node) AS ?osm_node)
    (SAMPLE(?_osm_way) AS ?osm_way)
    (SAMPLE(?_osm_rel) AS ?osm_rel)
    ?etymology
    (?etymology AS ?from_entity)
    (wdt:${indirectProperty} AS ?from_prop)
    (false AS ?from_osm)
WHERE {
    ?etymology p:${indirectProperty} ?stmt.
    ?stmt ps:${indirectProperty} ?item.
    ?item p:P625 ?locationStatement;
          rdfs:label ?anyLabel.
    MINUS { ?stmt pq:P582 []. } # Ignore the etymology statement has an end date
    MINUS { ?item (wdt:P582|wdt:P585|wdt:P576|wdt:P3999) []. } # Ignore if the feature has an end date
    MINUS { ?etymology (wdt:${indirectProperty}/(wdt:P131|wdt:P276|wdt:P195)) ?item. } # Prevent duplicated etymologies where both the item and the item's containing location are linked through the indirect property
    MINUS { ?locationStatement pq:P582 []. } # Ignore if the location statement has an end date
    
    # Filter by location
    ?locationStatement ps:P625 ?_location.
    # FILTER(geo:sfContains( # https://opengeospatial.github.io/ogc-geosparql/geosparql11/spec.html#_f_2_1_query_parameters
    #     ?_location,
    #     "POLYGON((${southLon} ${westLat}, ${southLon} ${eastLat}, ${northLon} ${eastLat}, ${northLon} ${westLat}, ${southLon} ${westLat}))"^^geo:wktLiteral
    # )) #! Not yet supported by QLever
    BIND ("POINT(${centerLon} ${centerLat})"^^geo:wktLiteral AS ?center)
    FILTER (geof:distance(?_location, ?center) <= ${maxDistanceKm})
    
    OPTIONAL { ?item rdfs:label ?langLabel FILTER(LANG(?langLabel) = '${language}'). }
    OPTIONAL { ?item rdfs:label ?defaultLabel FILTER(LANG(?defaultLabel) = '${defaultLanguage}'). }
    OPTIONAL { ?item schema:description ?_itemDescription FILTER(LANG(?_itemDescription) = '${language}'). }
    OPTIONAL { ?item wdt:P373 ?_commons. }
    OPTIONAL { ?_wikipedia schema:about ?item; schema:isPartOf <https://${language}.wikipedia.org/>. }
    OPTIONAL { ?item wdt:P18 ?_picture. }
    OPTIONAL { ?item wdt:P11693 ?_osm_node. }
    OPTIONAL { ?item wdt:P10689 ?_osm_way. }
    OPTIONAL { ?item wdt:P402 ?_osm_rel. }
}
GROUP BY ?item ?etymology
${limit}
