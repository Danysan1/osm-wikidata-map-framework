PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>

SELECT
    ?item
    (SAMPLE(COALESCE(?lang_label,?default_label)) AS ?itemLabel)
    (SAMPLE(?_itemDescription) AS ?itemDescription)
    (SAMPLE(?_commons) AS ?itemCommons)
    (SAMPLE(?_wikipedia) AS ?wikipedia)
    (SAMPLE(?_itemPicture) AS ?itemPicture)
    (SAMPLE(?_osm_node) AS ?osm_node)
    (SAMPLE(?_osm_way) AS ?osm_way)
    (SAMPLE(?_osm_rel) AS ?osm_rel)
    (false AS ?from_osm)
    (true AS ?from_wikidata)
    (SAMPLE(?_location) AS ?location)
WHERE {
    ?item p:P625 ?locationStatement.
    ${wikidataCountryQuery}
    MINUS { ?item (wdt:P580|wdt:P571|wdt:P1619) ?startDate FILTER(YEAR(?startDate) > ${year}). } # Ignore if the feature has a bad start date
    MINUS { ?item (wdt:P582|wdt:P576|wdt:P3999) ?endDate FILTER(YEAR(?endDate) < ${year}). } # Ignore if the feature has a bad end date
    MINUS { ?item (wdt:P585|wdt:P376) []. } # Ignore if the feature isn't on Earth
    MINUS { ?locationStatement pq:P582 ?endDate FILTER(YEAR(?endDate) < ${year}). } # Ignore if the location statement has a bad end date
    ?locationStatement ps:P625 ?_location.

    # # Filter by location (geo:sfWithin)
    # BIND ("POLYGON((${westLon} ${southLat}, ${westLon} ${northLat}, ${eastLon} ${northLat}, ${eastLon} ${southLat}, ${westLon} ${southLat}))"^^geo:wktLiteral AS ?bbox)
    # ?_location geo:sfWithin ?bbox.
    
    # # Filter by location (geof:latitude/longitude)
    BIND(geof:latitude(?_location) AS ?lat)
    BIND(geof:longitude(?_location) AS ?lon)
    FILTER (?lat > ${southLat} && ?lat < ${northLat} && ?lon > ${westLon} && ?lon < ${eastLon})
    
    # # Filter by location (geof:distance)
    # BIND ("POINT(${centerLon} ${centerLat})"^^geo:wktLiteral AS ?center)
    # FILTER (geof:distance(?_location, ?center) <= ${maxDistanceKm})
    
    # # Extra details
    OPTIONAL { ?item @${language}@rdfs:label ?lang_label. }
    OPTIONAL { ?item @en@rdfs:label ?default_label. }
    OPTIONAL { ?item @${language}@schema:description ?_itemDescription. }
    OPTIONAL { ?item wdt:P910?/wdt:P373 ?_commons. }
    OPTIONAL { ?_wikipedia schema:about ?item; schema:isPartOf <https://${language}.wikipedia.org/>. }
    OPTIONAL { ?item wdt:P18 ?_itemPicture. }
    OPTIONAL { ?item wdt:P11693 ?_osm_node. }
    OPTIONAL { ?item wdt:P10689 ?_osm_way. }
    OPTIONAL { ?item wdt:P402 ?_osm_rel. }
}
GROUP BY ?item
${limit}
