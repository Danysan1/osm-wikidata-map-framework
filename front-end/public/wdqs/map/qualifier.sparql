#defaultView:Map
SELECT DISTINCT
    ?etymology
    ?location
    ?commons
    (?_linkPicture AS ?linkPicture)
    ?osm_node_id
    ?osm_way_id
    ?osm_rel_id
    (?etymology AS ?from_entity)
    (wdt:${indirectProperty} AS ?from_prop)
WHERE {
    ?etymology p:${indirectProperty} ?stmt.
    MINUS { ?stmt pq:P582 []. } # Ignore if the etymology statement has an end date
    MINUS { ?stmt wikibase:rank wikibase:DeprecatedRank. }
    ${entityFilterQuery}
    
    # Filter by location
    SERVICE wikibase:box {
        ?stmt pq:P625 ?location.
        bd:serviceParam wikibase:cornerWest 'POINT(${westLon} ${southLat})'^^geo:wktLiteral;
                        wikibase:cornerEast 'POINT(${eastLon} ${northLat})'^^geo:wktLiteral.
    } # https://www.mediawiki.org/wiki/Wikidata_Query_Service/User_Manual#Search_within_box

    OPTIONAL { ?stmt pq:P373 ?commons. }
    ${linkPictureQuery}
    OPTIONAL { ?stmt pq:P11693 ?osm_node_id. }
    OPTIONAL { ?stmt pq:P10689 ?osm_way_id. }
    OPTIONAL { ?stmt pq:P402 ?osm_rel_id. }
}
${limit}
