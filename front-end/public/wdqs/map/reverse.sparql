#defaultView:Map
SELECT
    ?item
    (SAMPLE(COALESCE(?lang_label,?default_label,?anyLabel)) AS ?itemLabel)
    (SAMPLE(?_itemDescription) AS ?itemDescription)
    (SAMPLE(?_location) AS ?location)
    (SAMPLE(?_commons) AS ?itemCommons)
    (SAMPLE(?_website) AS ?website)
    (SAMPLE(?_wikipedia) AS ?wikipedia)
    (SAMPLE(?_wikispore) AS ?wikispore)
    (SAMPLE(?_linkPicture) AS ?linkPicture)
    (SAMPLE(?_itemPicture) AS ?itemPicture)
    (SAMPLE(?_height) AS ?height)
    (SAMPLE(?_levels) AS ?levels)
    (SAMPLE(?_osm_node_id) AS ?osm_node_id)
    (SAMPLE(?_osm_way_id) AS ?osm_way_id)
    (SAMPLE(?_osm_rel_id) AS ?osm_rel_id)
    (SAMPLE(?_alias) AS ?alias)
    ?etymology
    (?etymology AS ?from_entity)
    (wdt:${indirectProperty} AS ?from_prop)
WHERE {
    ?etymology p:${indirectProperty} ?stmt.
    ${entityFilterQuery}
    ?stmt ps:${indirectProperty} ?item.
    ${featureFilterQuery}
    ?item p:P625 ?locationStatement.
    ${wikidataCountryQuery}
    MINUS { ?stmt pq:P582 []. } # Ignore if the entity link statement has an end date
    MINUS { ?stmt wikibase:rank wikibase:DeprecatedRank. } # Ignore if the entity link statement is deprecated
    MINUS { ?item (wdt:P582|wdt:P585|wdt:P576|wdt:P3999|wdt:P376) []. } # Ignore if the feature has an end date or isn't on Earth
    MINUS { ?etymology (wdt:${indirectProperty}/(wdt:P131|wdt:P276|wdt:P195)) ?item. } # Prevent duplicated linked entities where both the item and the item's containing location are linked through the indirect property
    MINUS { ?locationStatement pq:P582 []. } # Ignore if the location statement has an end date

    # Filter by location
    SERVICE wikibase:box {
        ?locationStatement ps:P625 ?_location.
        bd:serviceParam wikibase:cornerWest 'POINT(${westLon} ${southLat})'^^geo:wktLiteral;
                        wikibase:cornerEast 'POINT(${eastLon} ${northLat})'^^geo:wktLiteral.
    } # https://www.mediawiki.org/wiki/Wikidata_Query_Service/User_Manual#Search_within_box
    
    ?item rdfs:label ?anyLabel.
    OPTIONAL { ?item rdfs:label ?lang_label FILTER(LANG(?lang_label) = '${language}'). }
    OPTIONAL { ?item rdfs:label ?default_label FILTER(LANG(?default_label) = 'mul'). }
    OPTIONAL { ?item schema:description ?_itemDescription FILTER(LANG(?_itemDescription) = '${language}'). }
    OPTIONAL { ?item wdt:P910?/wdt:P373 ?_commons. }
    OPTIONAL { ?item wdt:P856 ?_website. }
    OPTIONAL { ?_wikipedia schema:about ?item; schema:isPartOf <https://${language}.wikipedia.org/>. }
    OPTIONAL { ?item wdt:P7721 ?_wikispore. }
    ${linkPictureQuery}
    OPTIONAL { ?item wdt:P18 ?_itemPicture. }
    OPTIONAL { ?item p:P2048/psn:P2048/wikibase:quantityAmount ?_height. } # https://stackoverflow.com/a/57560572/2347196
    OPTIONAL { ?item wdt:P1101 ?_levels. }
    OPTIONAL { ?item wdt:P11693 ?_osm_node_id. }
    OPTIONAL { ?item wdt:P10689 ?_osm_way_id. }
    OPTIONAL { ?item wdt:P402 ?_osm_rel_id. }
    OPTIONAL { ?_alias owl:sameAs ?item. }
}
GROUP BY ?item ?etymology
${limit}
