SELECT ?wikidata
    (SAMPLE(?rawWikidata) AS ?alias)
    (SAMPLE(?lang_label) AS ?lang_name)
    (COALESCE(SAMPLE(?label_mul), SAMPLE(?label_en)) AS ?default_name)
    (SAMPLE(?_description) AS ?description)
    (SAMPLE(?_instanceID) AS ?instanceID)
    (SAMPLE(?instance_name) AS ?instance)
    (SAMPLE(?_genderID) AS ?genderID)
    (SAMPLE(?gender_name) AS ?gender)
    (SAMPLE(?_wikipedia) AS ?wikipedia)
    (SAMPLE(?_wikispore) AS ?wikispore)
    (SAMPLE(?_commons) AS ?commons)
    (GROUP_CONCAT(DISTINCT ?occupation_name; SEPARATOR=', ') AS ?occupations)
    (SAMPLE(?_countryID) AS ?countryID)
    (GROUP_CONCAT(DISTINCT ?country_name; SEPARATOR=', ') AS ?citizenship)
    (GROUP_CONCAT(DISTINCT ?picture; SEPARATOR='||') AS ?pictures)
    (GROUP_CONCAT(DISTINCT ?prize_name; SEPARATOR=', ') AS ?prizes)
    (SAMPLE(?_event_date) AS ?event_date)
    (SAMPLE(?_event_date_precision) AS ?event_date_precision)
    (SAMPLE(?_start_date) AS ?start_date)
    (SAMPLE(?_start_date_precision) AS ?start_date_precision)
    (SAMPLE(?_end_date) AS ?end_date)
    (SAMPLE(?_end_date_precision) AS ?end_date_precision)
    (SAMPLE(?birth_date) AS ?birth_date)
    (SAMPLE(?birth_date_precision) AS ?birth_date_precision)
    (SAMPLE(?death_date) AS ?death_date)
    (SAMPLE(?death_date_precision) AS ?death_date_precision)
    (GROUP_CONCAT(DISTINCT ?event_place_name; SEPARATOR=', ') AS ?event_place)
    (SAMPLE(?birth_place_name) AS ?birth_place)
    (SAMPLE(?death_place_name) AS ?death_place)
    (SAMPLE(?_wkt_coords) AS ?wkt_coords)
    (GROUP_CONCAT(DISTINCT ?part; SEPARATOR=';') AS ?parts)
WHERE {
    VALUES ?rawWikidata { ${wikidataValues} }
    ?rawWikidata owl:sameAs? ?wikidata.

    # ID and name of the class of the entity
    OPTIONAL { # instance of - https://www.wikidata.org/wiki/Property:P31
        ?_instanceID ^wdt:P31 ?wikidata;
            rdfs:label ?instance_name.
        FILTER(lang(?instance_name)='${language}').
    }

    # Name and description of the entity
    OPTIONAL {
        ?wikidata rdfs:label ?lang_label.
        FILTER(lang(?lang_label)='${language}').
    }
    
    OPTIONAL {
        ?wikidata schema:description ?_description.
        FILTER(lang(?_description)='${language}').
    }

    OPTIONAL {
        ?wikidata rdfs:label ?label_en.
        FILTER(lang(?label_en)='en').
    }

    OPTIONAL {
        ?wikidata rdfs:label ?label_mul.
        FILTER(lang(?label_mul)='mul').
    }

    # Name of the occupations of this entity
    OPTIONAL {
        ?occupation ^wdt:P106 ?wikidata.
        {
            ?gender ^wdt:P21 ?wikidata.
            FILTER(?gender IN (wd:Q6581072, wd:Q1052281)). # female / transgender female
            ?occupation_name ^wdt:P2521 ?occupation. # female form of occupation label
        } UNION {
            MINUS {
                ?occupation_name ^wdt:P2521 ?occupation.
                FILTER(lang(?occupation_name)='${language}').
            }. # female form of occupation is NOT available in this language
            ?gender ^wdt:P21 ?wikidata.
            FILTER(?gender IN (wd:Q6581072, wd:Q1052281)). # female / transgender female
            ?occupation_name ^rdfs:label ?occupation. # base occupation label
        } UNION {
            ?gender ^wdt:P21 ?wikidata.
            FILTER(?gender NOT IN (wd:Q6581072, wd:Q1052281)). # NOT female / transgender female
            ?occupation_name ^wdt:P3321 ?occupation. # male form of occupation label
        } UNION {
            MINUS {
                ?occupation_name ^wdt:P3321 ?occupation.
                FILTER(lang(?occupation_name)='${language}').
            }. # male form of occupation is NOT available in this language
            ?gender ^wdt:P21 ?wikidata.
            FILTER(?gender NOT IN (wd:Q6581072, wd:Q1052281)). # NOT female / transgender female
            ?occupation_name ^rdfs:label ?occupation. # male form of occupation label
        } UNION {
            MINUS { ?wikidata wdt:P21 []. } . # no gender specified
            ?occupation_name ^rdfs:label ?occupation. # base occupation label
        }
        FILTER(lang(?occupation_name)='${language}').
    }

    # Pictures of this entity
    OPTIONAL {
        ?wikidata wdt:P18|wdt:P94|wdt:P242|wdt:P15|wdt:P41 ?picture.
# picture - https://www.wikidata.org/wiki/Property:P18
# coat of arms image - https://www.wikidata.org/wiki/Property:P94
# locator map image - https://www.wikidata.org/wiki/Property:P242
# route map - https://www.wikidata.org/wiki/Property:P15
# flag - https://www.wikidata.org/wiki/Property:P41
    }

    # ID and name of the gender of the entity
    OPTIONAL {
        ?_genderID ^wdt:P21 ?wikidata;
            rdfs:label ?gender_name.
        FILTER(lang(?gender_name)='${language}').
    }

    # Date the event represented by this entity took place
    OPTIONAL {
        ?wikidata p:P585/psv:P585 [ # event date - https://www.wikidata.org/wiki/Property:P585
            wikibase:timePrecision ?_event_date_precision;
            wikibase:timeValue ?_event_date
        ].
        MINUS {
            ?wikidata p:P585/psv:P585/wikibase:timePrecision ?other_event_date_precision.
            FILTER (?other_event_date_precision > ?_event_date_precision).
        }.
    }

    # Date the event represented by this entity started
    OPTIONAL {
        ?wikidata (p:P580/psv:P580)|(p:P571/psv:P571)|(p:P1619/psv:P1619) [
# start time - https://www.wikidata.org/wiki/Property:P580
# inception - https://www.wikidata.org/wiki/Property:P571
# date of official opening - https://www.wikidata.org/wiki/Property:P1619
            wikibase:timePrecision ?_start_date_precision;
            wikibase:timeValue ?_start_date
        ].
        MINUS {
            ?wikidata (p:P571/psv:P571/wikibase:timePrecision)|(p:P1619/psv:P1619/wikibase:timePrecision)|(p:P569/psv:P569/wikibase:timePrecision) ?other_start_date_precision.
            FILTER (?other_start_date_precision > ?_start_date_precision).
        }.
    }

    # Date the event represented by this entity ended
    OPTIONAL {
        ?wikidata (p:P582/psv:P582)|(p:P576/psv:P576)|(p:P3999/psv:P3999) [
# end time - https://www.wikidata.org/wiki/Property:P582
# dissolved, abolished or demolished date - https://www.wikidata.org/wiki/Property:P576
# date of official closure - https://www.wikidata.org/wiki/Property:P3999
            wikibase:timePrecision ?_end_date_precision;
            wikibase:timeValue ?_end_date
        ].
        MINUS {
            ?wikidata (p:P582/psv:P582/wikibase:timePrecision)|(p:P576/psv:P576/wikibase:timePrecision)|(p:P3999/psv:P3999/wikibase:timePrecision) ?other_end_date_precision.
            FILTER (?other_end_date_precision > ?_end_date_precision).
        }.
    }

    # Date the person represented by this entity was born in
    OPTIONAL {
        ?wikidata p:P569/psv:P569 [ # birth date - https://www.wikidata.org/wiki/Property:P569
            wikibase:timePrecision ?birth_date_precision;
            wikibase:timeValue ?birth_date
        ].
        MINUS {
            ?wikidata p:P569/psv:P569/wikibase:timePrecision ?other_birth_date_precision.
            FILTER (?other_birth_date_precision > ?birth_date_precision).
        }.
    }

    # Date the person represented by this entity died in
    OPTIONAL {
        ?wikidata p:P570/psv:P570 [ # death date - https://www.wikidata.org/wiki/Property:P570
            wikibase:timePrecision ?death_date_precision;
            wikibase:timeValue ?death_date
        ].
        MINUS {
            ?wikidata p:P570/psv:P570/wikibase:timePrecision ?other_death_date_precision.
            FILTER (?other_death_date_precision > ?death_date_precision).
        }.
    }

    OPTIONAL {
        ?event_place_name ^rdfs:label/^wdt:P276 ?wikidata.
        FILTER(lang(?event_place_name)='${language}').
    }

    OPTIONAL {
        ?birth_place_name ^rdfs:label/^wdt:P19 ?wikidata.
        FILTER(lang(?birth_place_name)='${language}').
    }

    OPTIONAL {
        ?death_place_name ^rdfs:label/^wdt:P20 ?wikidata.
        FILTER(lang(?death_place_name)='${language}').
    }

    OPTIONAL {
        ?_wikipedia schema:about ?wikidata;
            schema:inLanguage '${language}';
            schema:isPartOf [ wikibase:wikiGroup 'wikipedia' ].
    }

    OPTIONAL {
        ?wikidata wdt:P910?/wdt:P373 ?_commons.
    }

    OPTIONAL {
        ?wikidata wdt:P7721 ?_wikispore.
    }

    OPTIONAL {
        ?prize ^wdt:P166 ?wikidata # awarded prize
        {
            ?prize_group ^wdt:P361 ?prize.
            FILTER(?prize_group IN (wd:Q7191,wd:Q19020,wd:Q41254)). # Nobel, Academy (Oscar), Grammy
        } UNION {
            ?prize wdt:P31 wd:Q28444913. # Palme d'Or (Cannes)
        } UNION {
            ?prize wdt:P1027 wd:Q49024. # Golden Lion (Venice)
        }
        ?prize_name ^rdfs:label ?prize.
        FILTER(lang(?prize_name)='${language}').
    }

    OPTIONAL {
        ?wikidata wdt:P625 ?_wkt_coords.
        FILTER(STRSTARTS(?_wkt_coords, 'Point')).
    }

    OPTIONAL {
        ?_countryID ^wdt:P17|^wdt:P27 ?wikidata;
            rdfs:label ?country_name FILTER(lang(?country_name)='${language}').
    }

    OPTIONAL {
        VALUES ?groupType { wd:Q16334295 wd:Q10648343 wd:Q219160 } # Group of humans / Duo / Couple
        ?wikidata wdt:P31/wdt:P279? ?groupType;
                  wdt:P527 ?part.
        MINUS {
          VALUES ?notGroupType { wd:Q41710 wd:Q2472587 wd:Q33829 } # ethnic group / people / human population
          ?wikidata wdt:P31 ?notGroupType.
        }
    }
}
GROUP BY ?wikidata