#defaultView:Map
SELECT
    ?item
    (SAMPLE(COALESCE(?lang_label,?default_label,?anyLabel)) AS ?itemLabel)
    (SAMPLE(?_itemDescription) AS ?itemDescription)
    (SAMPLE(?_location) AS ?location)
    (SAMPLE(?_commons) AS ?commons)
    (SAMPLE(?_wikipedia) AS ?wikipedia)
    (SAMPLE(?_wikispore) AS ?wikispore)
    (SAMPLE(?_picture) AS ?picture)
    (SAMPLE(?_height) AS ?height)
    (SAMPLE(?_levels) AS ?levels)
    (SAMPLE(?_osm_node) AS ?osm_node)
    (SAMPLE(?_osm_way) AS ?osm_way)
    (SAMPLE(?_osm_rel) AS ?osm_rel)
    (SAMPLE(?_alias) AS ?alias)
    (SAMPLE(?_statementEntity) AS ?statementEntity)
    ?etymology
    (?etymology AS ?from_entity)
    (wdt:${indirectProperty} AS ?from_prop)
WITH { 
    SELECT ?etymology ?_location ?_commons ?_picture ?_osm_node ?_osm_way ?_osm_rel ?_statementEntity
    WHERE {
        ?etymology p:${indirectProperty} ?stmt.
        MINUS { ?stmt pq:P582 []. } # Ignore if the etymology statement has an end date
        MINUS { ?etymology (wdt:${indirectProperty}/wdt:P373/^pq:P373) ?stmt} # Prevent duplicated etymologies when the etymology has another link that points to the exact entity for the item (see https://commons.wikimedia.org/wiki/Commons_talk:WikiProject_P%C3%A8re-Lachaise_Cemetery )
        MINUS { ?stmt wikibase:rank wikibase:DeprecatedRank. }
        
        # Filter by location
        SERVICE wikibase:box {
            ?stmt pq:P625 ?_location.
            bd:serviceParam wikibase:cornerWest 'POINT(${westLon} ${southLat})'^^geo:wktLiteral;
                            wikibase:cornerEast 'POINT(${eastLon} ${northLat})'^^geo:wktLiteral.
        } # https://www.mediawiki.org/wiki/Wikidata_Query_Service/User_Manual#Search_within_box

        OPTIONAL { ?stmt pq:P373 ?_commons. }
        ${pictureQuery}
        OPTIONAL { ?stmt pq:P11693 ?_osm_node. }
        OPTIONAL { ?stmt pq:P10689 ?_osm_way. }
        OPTIONAL { ?stmt pq:P402 ?_osm_rel. }
        OPTIONAL { ?stmt pq:P805 ?_statementEntity. }
    }
} AS %qualifier
WITH {
    SELECT ?etymology ?_location ?item ?anyLabel ?lang_label ?default_label ?_itemDescription ?_commons ?_wikipedia ?_wikispore ?_picture ?_height ?_levels ?_osm_node ?_osm_way ?_osm_rel ?_alias
    WHERE {
        ?etymology p:${indirectProperty} ?stmt.
        ?stmt ps:${indirectProperty} ?item.
        ${classFilterQuery}
        ?item p:P625 ?locationStatement.
        ${wikidataCountryQuery}
        MINUS { ?stmt (pq:P625|pq:P582) []. } # Ignore if the etymology statement has a location qualifier (it will fetched be in the other subquery) or an end date
        MINUS { ?stmt wikibase:rank wikibase:DeprecatedRank. }
        MINUS { ?etymology (wdt:${indirectProperty}/wdt:P373/^pq:P373) ?stmt. } # Prevent duplicated etymologies when the etymology has another link that points to the exact entity for the item (see https://commons.wikimedia.org/wiki/Commons_talk:WikiProject_P%C3%A8re-Lachaise_Cemetery )
        MINUS { ?item (wdt:P582|wdt:P585|wdt:P576|wdt:P3999|wdt:P376) []. } # Ignore if the feature has an end date or isn't on Earth
        MINUS { ?etymology (wdt:${indirectProperty}/(wdt:P131|wdt:P276|wdt:P195)) ?item. } # Prevent duplicated etymologies where both the item and the item's containing location are linked through the indirect property
        MINUS { ?locationStatement pq:P582 []. } # Ignore if the location statement has an end date

        # Filter by location
        SERVICE wikibase:box {
            ?locationStatement ps:P625 ?_location.
            bd:serviceParam wikibase:cornerWest 'POINT(${westLon} ${southLat})'^^geo:wktLiteral;
                            wikibase:cornerEast 'POINT(${eastLon} ${northLat})'^^geo:wktLiteral.
        } # https://www.mediawiki.org/wiki/Wikidata_Query_Service/User_Manual#Search_within_box
        
        ?item rdfs:label ?anyLabel.
        OPTIONAL { ?item rdfs:label ?lang_label FILTER(LANG(?lang_label) = '${language}'). }
        OPTIONAL { ?item rdfs:label ?default_label FILTER(LANG(?default_label) = 'mul'). }
        OPTIONAL { ?item schema:description ?_itemDescription FILTER(LANG(?_itemDescription) = '${language}'). }
        OPTIONAL { ?item wdt:P910?/wdt:P373 ?_commons. }
        OPTIONAL { ?_wikipedia schema:about ?item; schema:isPartOf <https://${language}.wikipedia.org/>. }
        OPTIONAL { ?item wdt:P7721 ?_wikispore. }
        OPTIONAL { ?item wdt:P18 ?_picture. }
        OPTIONAL { ?item p:P2048/psn:P2048/wikibase:quantityAmount ?_height. } # https://stackoverflow.com/a/57560572/2347196
        OPTIONAL { ?item wdt:P1101 ?_levels. }
        OPTIONAL { ?item wdt:P11693 ?_osm_node. }
        OPTIONAL { ?item wdt:P10689 ?_osm_way. }
        OPTIONAL { ?item wdt:P402 ?_osm_rel. }
        OPTIONAL { ?_alias owl:sameAs ?item. }
    }
} AS %reverse
WHERE {
    { INCLUDE %qualifier. } UNION { INCLUDE %reverse. }
}
GROUP BY ?item ?etymology
${limit}
