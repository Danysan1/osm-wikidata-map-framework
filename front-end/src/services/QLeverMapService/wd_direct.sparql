PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
PREFIX geof: <http://www.opengis.net/def/function/geosparql/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX wikibase: <http://wikiba.se/ontology#>

SELECT
    ?item
    ?etymology
    (SAMPLE(COALESCE(?lang_label,?default_label)) AS ?itemLabel)
    (SAMPLE(?_itemDescription) AS ?itemDescription)
    (SAMPLE(?_commons) AS ?commons)
    (SAMPLE(?_wikipedia) AS ?wikipedia)
    (SAMPLE(?_picture) AS ?picture)
    (SAMPLE(?_osm_node) AS ?osm_node)
    (SAMPLE(?_osm_way) AS ?osm_way)
    (SAMPLE(?_osm_rel) AS ?osm_rel)
    (SAMPLE(?_statementEntity) AS ?statementEntity)
    (?item AS ?from_entity)
    (p:${directProperty} AS ?from_prop) # TODO Use SAMPLE(?_from_p) when VALUES is supported by QLever
    (false AS ?from_osm)
    (true AS ?from_wikidata)
    (SAMPLE(?_location) AS ?location)
WHERE {
    # VALUES (?_from_p ?_from_ps) { ${directPropertyValues} } # TODO Use VALUES when supported by QLever
    # ?item p:P625 ?locationStatement;
    #       ?_from_p ?stmt.
    ?item p:P625 ?locationStatement;
          p:${directProperty} ?stmt.

    ${wikidataCountryQuery}
    MINUS { ?item (wdt:P582|wdt:P585|wdt:P576|wdt:P3999|wdt:P376) ?_itemEndDate. } # Ignore if the feature has an end date or isn't on Earth # TODO Use blank nodes
    MINUS { ?locationStatement pq:P582 ?_locationEndDate. } # Ignore if the location statement has an end date # TODO Use blank nodes
    MINUS { ?stmt wikibase:rank wikibase:DeprecatedRank. }
    ?locationStatement ps:P625 ?_location.
    
    # # Filter by location (geo:sfWithin)
    # BIND ("POLYGON((${westLon} ${southLat}, ${westLon} ${northLat}, ${eastLon} ${northLat}, ${eastLon} ${southLat}, ${westLon} ${southLat}))"^^geo:wktLiteral AS ?bbox)
    # ?_location geo:sfWithin ?bbox.
    
    # # Filter by location (geof:latitude/longitude)
    BIND(geof:latitude(?_location) AS ?lat)
    BIND(geof:longitude(?_location) AS ?lon)
    FILTER (?lat > ${southLat} && ?lat < ${northLat} && ?lon > ${westLon} && ?lon < ${eastLon})
    
    # # Filter by location (geof:distance)
    # BIND ("POINT(${centerLon} ${centerLat})"^^geo:wktLiteral AS ?center)
    # FILTER (geof:distance(?_location, ?center) <= ${maxDistanceKm})

    # ?stmt ?_from_ps ?etymology. # TODO Use VALUES when supported by QLever
    ?stmt ps:${directProperty} ?etymology.

    # # Extra details
    OPTIONAL { ?item @${language}@rdfs:label ?lang_label. }
    OPTIONAL { ?item @en@rdfs:label ?default_label. }
    OPTIONAL { ?item @${language}@schema:description ?_itemDescription. }
    OPTIONAL { ?item wdt:P910?/wdt:P373 ?_commons. }
    OPTIONAL { ?_wikipedia schema:about ?item; schema:isPartOf <https://${language}.wikipedia.org/>. }
    OPTIONAL { ?item wdt:P18 ?_picture. }
    OPTIONAL { ?item wdt:P11693 ?_osm_node. }
    OPTIONAL { ?item wdt:P10689 ?_osm_way. }
    OPTIONAL { ?item wdt:P402 ?_osm_rel. }
    OPTIONAL { ?stmt pq:P805 ?_statementEntity. }
}
GROUP BY ?item ?etymology
${limit}
