PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wikibase: <http://wikiba.se/ontology#>

SELECT DISTINCT
    ?item
    (COALESCE(?label_mul, ?label_en) AS ?itemLabel)
    ?commons
    ?location
    ?etymology
    ?from_prop
    ?alias
WHERE {
    VALUES (?_from_p ?_from_ps) { ${directPropertyValues} }
    ?item p:P625 ?locationStatement;
          ?_from_p ?stmt.
    ${wikidataCountryQuery}
    MINUS { ?item (wdt:P582|wdt:P585|wdt:P576|wdt:P3999|wdt:P376) ?_itemEndDate. } # Ignore if the feature has an end date or isn't on Earth # TODO Use blank nodes
    MINUS { ?locationStatement pq:P582 ?_locationEndDate. } # Ignore if the location statement has an end date # TODO Use blank nodes
    MINUS { ?stmt wikibase:rank wikibase:DeprecatedRank. }

    ?stmt ?_from_ps ?etymology.
    FILTER (isIRI(?etymology) && !wikibase:isSomeValue(?etymology) && STRENDS(STR(?item),'${lastDigit}')) # Paging through last Q-ID digit because ORDER BY + LIMIT + OFFSET is too slow
    
    ?locationStatement ps:P625 ?location.
    OPTIONAL { ?item rdfs:label ?label_mul FILTER(LANG(?label_mul) = "mul") }
    OPTIONAL { ?item rdfs:label ?label_en FILTER(LANG(?label_en) = "en") }
    OPTIONAL { ?item wdt:P910?/wdt:P373 ?commons. }
    OPTIONAL { ?alias owl:sameAs ?item. }
}
